import org.apache.tools.ant.filters.ReplaceTokens

buildscript {
    repositories {
        mavenCentral()
        gradlePluginPortal()
    }

    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:6.1.0'
    }
}

plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
    id 'maven-publish' // Replace 'maven' plugin with 'maven-publish'
}

group 'me.realized'
version '3.2.8'

compileJava {
    options.encoding = 'UTF-8'
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

processResources {
    def group = project.group.toString() + "." + project.name.toLowerCase() + ".shaded"

    from(sourceSets.main.resources.srcDirs) {
        include '**/*.yml'
        filter(ReplaceTokens, tokens: [NAME: project.name, VERSION: project.version, SHADED_GROUP: group])
    }
}

repositories {
    mavenCentral()

    maven {
        name 'spigot-repo'
        url 'https://hub.spigotmc.org/nexus/content/repositories/public'
    }

    maven {
        name 'bungeecord-repo'
        url 'https://oss.sonatype.org/content/repositories/snapshots'
    }

    maven {
        name 'mvdw-repo'
        url 'http://repo.mvdw-software.be/content/groups/public/'
    }

    maven {
        name 'clip-repo'
        url 'http://repo.extendedclip.com/content/repositories/placeholderapi/'
    }

    maven {
        name 'codemc-repo'
        url 'https://repo.codemc.io/repository/maven-public/'
    }

    maven {
        name 'inventive-repo'
        url 'https://repo.inventivetalent.org/content/groups/public/'
    }

    flatDir {
        dirs "$rootDir/libs/"
    }
}

dependencies {
    implementation 'org.projectlombok:lombok:1.18.22'
    implementation 'org.spigotmc:spigot-api:1.14.4-R0.1-SNAPSHOT'
    implementation (name: 'MVdWPlaceholderAPI-3.1.1') {
        transitive = false
    }
    implementation name: 'Vault-1.6.7'
    implementation 'me.clip:placeholderapi:2.10.9'

    // shaded dependencies
    implementation 'redis.clients:jedis:3.5.1'
    implementation 'org.apache.commons:commons-pool2:2.4.2'
    implementation 'com.zaxxer:HikariCP:4.0.2'
    implementation 'org.slf4j:slf4j-nop:1.7.32'
    implementation 'org.bstats:bstats-bukkit:1.7'
}

clean.doFirst {
    delete "$rootDir/out/"
}

shadowJar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    destinationDirectory = file("$rootDir/out/")
    archiveBaseName.set('TokenManager')
    archiveClassifier.set('')
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    dependencies {
        include(dependency('redis.clients:.*'))
        include(dependency('org.apache.commons:.*'))
        include(dependency('com.zaxxer:.*'))
        include(dependency('org.slf4j:.*'))
        include(dependency('org.bstats:.*'))
    }

    def group = project.group.toString() + "." + project.name.toLowerCase() + ".shaded."

    relocate 'redis.clients', group + 'redis.clients'
    relocate 'org.apache.commons.pool2', group + 'apache.commons.pool2'
    relocate 'com.zaxxer.hikari', group + 'zaxxer.hikari'
    relocate 'org.slf4j', group + "slf4j"
    relocate 'org.bstats', group + 'bstats'
}

build.dependsOn(shadowJar)

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            url = uri("$buildDir/repo")
        }
    }
}
